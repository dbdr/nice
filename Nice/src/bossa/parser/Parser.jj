/**************************************************************************/
/*                                N I C E                                 */
/*             A high-level object-oriented research language             */
/*                        (c) Daniel Bonniot 2002                         */
/*                                                                        */
/*  This program is free software; you can redistribute it and/or modify  */
/*  it under the terms of the GNU General Public License as published by  */
/*  the Free Software Foundation; either version 2 of the License, or     */
/*  (at your option) any later version.                                   */
/*                                                                        */
/**************************************************************************/

/**
    JavaCC parser for Nice.
*/

options {
  JAVA_UNICODE_ESCAPE = true;
  DEBUG_LOOKAHEAD = false;
}

PARSER_BEGIN(Parser)

package bossa.parser;

import java.io.*;
import java.util.*;

import bossa.util.*;
import bossa.syntax.*;

import mlsub.typing.TypeSymbol;
import mlsub.typing.TypeConstructor;
import mlsub.typing.MonotypeVar;

class Parser
{
  private static IdentExp symb(String name, Token t, boolean quoted)
  {
    return new IdentExp(new LocatedString(name,new Location(t),quoted));
  }
  private static IdentExp symb(String name, Token t)
  {
    return symb(name,t,true);
  }
  private static IdentExp symb(Token t)
  {
    return symb(t.image,t);
  }
/*
  private static final Expression trueExp = 
    new IdentExp(new LocatedString("true", Location.nowhere()));

  private static final Expression falseExp = 
    new IdentExp(new LocatedString("false", Location.nowhere()));
*/
  static List globalDefinitions;
}
PARSER_END(Parser)

/***********************************************************************
 * Comments
 ***********************************************************************/

// picked from JavaCC-1.1.jj grammar out of JavaCC examples

/* WHITE SPACE */

SKIP : { " " | "\t" | "\n" | "\r" | "\f" }

/* COMMENTS */

MORE :
{
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

SPECIAL_TOKEN : 
{ <SINGLE_LINE_COMMENT: "//" (~["\n", "\r"])* ("\n" | "\r" | "\r\n" )? > }

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

/***********************************************************************
 * Reserved tokens
 ***********************************************************************/

TOKEN: /* Separators, Delimiters */
{
  < LPAR:       "("   >
| < RPAR:       ")"   >
| < LBRK:       "["   >
| < RBRK:       "]"   >
| < LBRACE:     "{"   >
| < RBRACE:     "}"   >
| < DOT:        "."   >
| < COLON:      ":"   >
| < COMMA:      ","   >
| < SEMI:       ";"   >
| < MAPSTO:     "->"  >
| < COND:       "?"   >
| < ANY:        "_"   >
| < BACKSLASH:  "\\"  >      
}

TOKEN: /* Operators */
{
  < EQ:      "="   >
| < PLUS:    "+"   >
| < MINUS:   "-"   >
// | < POWER:   "**"  >
| < MULT:    "*"   >
| < SLASH:   "/"   >
| < NEQ:     "!="  >
| < MOD:     "%"   >
| < LNOT:    "!"   >
| < BAND:    "&"   >
| < BOR:     "|"   >
| < BXOR:    "^"   >
| < BNOT:    "~"   >
| < LEQ:     "<="  >
| < GEQ:     ">="  >
| < EQEQ:    "=="  >
| < SUB:     "<:"  >
| < LT:      "<"   >
| < GT:      ">"   >
| < LAND:    "&&"  >
| < LOR:     "||"  >
//| < LSHIFT:  "<<"  >
// RSHIFT is not a token: operator >> is made of two consecutive tokens ">"
}

TOKEN: /* Keywords */
{
  < CLASS:      "class"		>
| < INTERFACE:  "interface"	>
| < IMPLEMENTS: "implements"	>
| < EXTENDS:    "extends"    	>

// Packages
| < IMPORT:     "import"     	>
| < PACKAGE:    "package"    	>

| < AT:         "@"          	>
| < EXACTLY_AT: "#"          	>
| < ANY_:	"Any"        	> /* Nice specific */

// Assertions
| < ASSERT:     "assert"     	>
| < REQUIRE:    "requires"     	>
| < ENSURE:     "ensures"     	>

// Expressions
| < FOR:        "for"        	>
| < IF:         "if"         	>
| < ELSE:       "else"       	>
| < WHILE:      "while"      	>
| < DO:		"do"		>
//| < TRUE:       "true"       	>
//| < FALSE:      "false"      	>
| < FUN:        "fun"        	> /* Nice specific */
| < ALIKE:      "alike"      	> /* Nice specific */
//| < THIS:       "this"       	>
| < SUPER:      "super"         >
| < INSTANCEOF: "instanceof" 	>
| < SYNCHRONIZED: "synchronized">
| < NEW:        "new"        	>
| < NULL:	"null"		>

| < BREAK:	"break"		>
| < CONTINUE:	"continue"	>

// Statements
| < RETURN:     "return"     	>
| < VARIABLE:   "var"   	> /* Nice specific */

// modifiers
| < PUBLIC:     "public"     	>
| < PRIVATE:    "private"    	>
| < PUBLICR:    "public-read" 	>
| < PRIVATEW:   "private-write"	>
| < PROTECTED:  "protected"  	>
| < STATIC:     "static"     	>
| < FINAL:      "final"      	>
| < TRANSIENT:  "transient"  	>
| < VOLATILE:   "volatile"   	>
| < CONST:      "const"      	>
| < NATIVE:     "native"     	> /* Nice specific */
| < INLINE:     "inline"        > /* Nice specific */
| < ABSTRACT:   "abstract"   	>

// exceptions
| < THROW:	"throw"		>
| < TRY:	"try" 		>
| < CATCH:	"catch" 	>
| < FINALLY:    "finally"    	>
}


/***********************************************************************
 * Literals
 ***********************************************************************/
TOKEN: /* Different kinds of digits */
{
  < #DIGIT:  ["0"-"9"] >                     // decimal
| < #XDIGIT: ["0"-"9", "a"-"f", "A"-"F"] >   // hexadecimal
| < #ODIGIT: ["0"-"7"] >		     // octal
| < #BDIGIT: ["0", "1"] >		     // binary
}


TOKEN :
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
}

TOKEN: /* Floating point literals */
{
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
}

TOKEN: /* Char literals */
{
  < CHARACTER_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )
      "'"
  >
}

TOKEN: /* String literals */
{
  < LSTRING:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
|
  < BACKQUOTEDSTRING:
      "`"
      (   (~["`","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "`"
  >
}

/***********************************************************************
 * Identifiers
 ***********************************************************************/
// identifiers are the same as in Java, except that:
// 2. the character ' is allowed
// XXX: conform to Java
TOKEN:
{
  < IDENT: <IDENT_START> (<IDENT_PART>)* >
| < #IDENT_PART:       (<IDENT_START> | <IDENT_DIGIT> | "'") >
| < #IDENT_START:
     [
      "\u0024",
      "\u0041"-"\u005a",
      "\u005f", 
      "\u0061"-"\u007a",
      "\u00c0"-"\u00d6",
      "\u00d8"-"\u00f6",
      "\u00f8"-"\u00ff",
      "\u0100"-"\u1fff",
      "\u3040"-"\u318f",
      "\u3300"-"\u337f",
      "\u3400"-"\u3d2d",
      "\u4e00"-"\u9fff",
      "\uf900"-"\ufaff"
     ]
  >
| < #IDENT_DIGIT:
    [
     "\u0030"-"\u0039",
     "\u0660"-"\u0669",
     "\u06f0"-"\u06f9",
     "\u0966"-"\u096f",
     "\u09e6"-"\u09ef",
     "\u0a66"-"\u0a6f",
     "\u0ae6"-"\u0aef",
     "\u0b66"-"\u0b6f",
     "\u0be7"-"\u0bef",
     "\u0c66"-"\u0c6f",
     "\u0ce6"-"\u0cef",
     "\u0d66"-"\u0d6f",
     "\u0e50"-"\u0e59",
     "\u0ed0"-"\u0ed9",
     "\u1040"-"\u1049"
    ]
  >
}
 
/*
 * Illegal tokens
 */
TOKEN:
{
  < ILLEGAL_TOKEN: (~[]) >
}

/***********************************************************************/
/*                          Grammar                                    */
/***********************************************************************/

String rawString():
{
  Token t;
}
{
  t=<LSTRING>
  { return t.image.substring(1, t.image.length() - 1); }
}

LocatedString ident():
{
  Token t;
}
{
  t=<IDENT> 
  { return new LocatedString(t.image,new Location(t)); }
}

String unlocIdent():
{
  Token t;
}
{
  t=<IDENT> 
  { return t.image; }
}

LocatedString identOrBackquoted():
{
  LocatedString res;
  Token name;
}
{
  (
   name=<BACKQUOTEDSTRING>
   { res = new LocatedString(name.image.substring(1, name.image.length() - 1),
			     new Location(name),
			     true); }
  |
   res = ident()
  )
  { return res; }
}

/* This allows idents as well as nice specific keywords,
   which is useful to specify native java package, class or method names
*/
Token doted_string_component():
{
  Token t;
}
{
  ( t = <IDENT> 
  | t = "Any"
  | t = "fun"
  | t = "alike"
  | t = "var"
  | t = "inline" 
  | t = "native" 
  )
  { return t; }
}

LocatedString doted_string():
{
  Token t;
  StringBuffer image;
  Location loc;
}
{
  t = <IDENT>
  { image = new StringBuffer(t.image); loc = new Location(t); }

  ( LOOKAHEAD(2) // needed to distinguish with ".class"
    "." t=doted_string_component() 
    { image.append(".").append(t.image); 
      loc.englobe(new Location(t)); } 
  )*

  { return new LocatedString(image.toString(), loc); }
}

/** Contains at least one "." */
LocatedString fullyQualifiedName():
{
  Token t;
  StringBuffer image;
  Location loc;
}
{
  t = <IDENT>
  { image = new StringBuffer(t.image); loc = new Location(t); }

  ( LOOKAHEAD(2) // needed to distinguish with ".class"
    "." t=doted_string_component() 
    { image.append(".").append(t.image); 
      loc.englobe(new Location(t)); } 
  )+

  { return new LocatedString(image.toString(), loc); }
}

LocatedString doted_array_string():
{
  LocatedString res;
}
{
  res=doted_string()
  ( "[]" { res.prepend("["); } )*
  { return res; }
}

Collection strings():
{
  Collection res=new ArrayList();
  LocatedString s;
}
{
  s=ident() { res.add(s); }
  ( "," s=ident() { res.add(s); } )*
  { return res; }
}

LocatedString floatingLiteral():
{
  Token t;
}
{
  t = <FLOATING_POINT_LITERAL> 
  { return new LocatedString(t.image, new Location(t)); }
}

LocatedString integerLiteral():
{
  Token t;
}
{
  t = <INTEGER_LITERAL> 
  { return new LocatedString(t.image, new Location(t)); }
}

ConstantExp typeExpression():
{
  LocatedString name;
}
{
  name = doted_array_string()
  { return ConstantExp.makeType(name); }
}

/***********************************************************************/
/*                          Type Symbols                               */
/***********************************************************************/

TypeIdent typeConstructorIdent():
{
  LocatedString name;
}
{
  name=doted_string()
  { return new TypeIdent(name); }
}

TypeConstructor typeConstructor():
{
  String name;
}
{
  name=unlocIdent()
  { return new TypeConstructor(name); }
}

TypeIdent typeIdent():
{
  LocatedString name;
}
{
  name=doted_string()
  { 
    return new TypeIdent(name);
  }
}

MonoSymbol monoSymbol():
{
  LocatedString name;
  Monotype type;
}
{
  type=monotype()
  name=ident()
  { return new MonoSymbol(name,type); }
}

List monoSymbols():
{
  List res=new ArrayList();
  MonoSymbol name;
}
{
  (
    name=monoSymbol() { res.add(name); }
    ( "," name=monoSymbol() { res.add(name); } )*
  )?
  { return res; }
}

/***********************************************************************/
/*                          Constraints                                */
/***********************************************************************/

TypeSymbol binder(Collection atomics):
{
  TypeIdent first;
  TypeConstructor second;
  MonotypeVar id;
}
{
  LOOKAHEAD( typeIdent() typeConstructor() )
  first=typeIdent() 
  second=typeConstructor()
  { 
    atomics.add(new TypeConstructorLeqCst(second,first)); 
    return second; 
  }
| { boolean sure = false; }
  [ "Any" | "!" { sure = true; } ] id=monotypeVar()
  { 
    if (sure)
      atomics.add(AtomicConstraint.sureTypeVar(id));
    return id; 
  }
}

List binders(Collection atomics):
{
    List res=new ArrayList();
    TypeSymbol t;
}
{
    t=binder(atomics) { res.add(t); }
    ( "," t=binder(atomics) { res.add(t); } )*
    { return res; }
}

AtomicConstraint atomicConstraint():
{
  Monotype m1,m2;  
  TypeIdent t;
  TypeIdent i;
  AtomicConstraint res;
}
{
(
  LOOKAHEAD(2)
  (
   m1=monotype()
   "<:"
   m2=monotype()
   { res=new MonotypeLeqCst(m1,m2); }
  )
|
  (
   t=typeConstructorIdent()
   ":"
   i=typeIdent()
   { res=new ImplementsCst(t,i); }
  )
)
  { return res; }
}

void atomics(List res):
// if res is null, we just want lookahead
{
  AtomicConstraint k;
}
{
  k=atomicConstraint() { if(res!=null) res.add(k); }
  ( "," k=atomicConstraint() { if(res!=null) res.add(k); } ) *
}

Constraint nonVoidConstraint():
{
  List b=null;
  List atoms=new ArrayList();
}
{
  "<" b=binders(atoms) [ "|" atomics(atoms) ] ">" 
  { return new Constraint(b,atoms); }
}

Constraint constraint():
{
  Constraint res;
}
{
  ( 
    res = nonVoidConstraint()
  |
    { res = Constraint.True; }
  )
  { return res; }
}

/***********************************************************************/
/*                          Types                                      */
/***********************************************************************/

//Monotypes

MonotypeVar monotypeVar():
{
  String name;
}
{
  name=unlocIdent()
  { return new MonotypeVar(name); }
}

Monotype monotypeConstructor():
{
    TypeIdent t;
    List p=null;
    Token first,last;
}
{
  { first=getToken(1); }

  // monotypeConstructors with no type parameters (and no "<" ">")
  // are parsed as MonotypeVars, not here.
  // This is fixed during scoping.

    t=typeConstructorIdent() 
    "<" [  p=monotypes() ] ">"
    { if(p==null) p=new ArrayList(0);
      last=getToken(0);

      return new MonotypeConstructor(t,new TypeParameters(p),
				     new Location(first, last)); 
    }
}

Monotype funOrTupleMonotype():
{
  Monotype codomain;
  List domain = null;
  boolean maybe = false;
  Token start,end;
}
{
  start="(" [ domain = monotypes() ] end=")" 
  [ LOOKAHEAD("?"|"->")
  [ "?" { maybe = true; } ]
  "->"
  codomain = monotype()
  { 
    FunType res = new FunType(domain, codomain); 
    res.nullness = maybe ? res.maybe : res.absent;
    return res; 
  }
  ]
  { 
    if (domain != null && domain.size() == 1)
      // (T) is T, the parenthesis helping to resolve ambiguities
      {
        return (Monotype) domain.get(0);
      }
    TupleType res = new TupleType(domain, new Location(start, end)); 
    res.nullness = res.absent;
    return res;
  }
}

Monotype monotype():
{ Monotype res; }
{

  res = simpleMonotype()
  res = arrayMonotype(res)
  
[ LOOKAHEAD(1)
  { boolean maybe = false; }
  [ "?" { maybe = true; } ]
  "->"
  { Monotype codomain; } codomain = monotype()
  { 
    Monotype[] domain;
    // Make void->... mean the same as ()->...
    if (res.isVoid())
      domain = null;
    else
      domain = new Monotype[]{ res };
    res = new FunType(domain, codomain); 
    res.nullness = maybe ? res.maybe : res.absent; }
]

  { return res; }
}

/**
   Parse an array type.
   Uses a recursive call to reverse the order of arrays.
   This is necessary since
     T[?][]
   is equivalent to
     ?Array<Array<T>>
   not
     Array<?Array<T>>
*/
Monotype arrayMonotype(Monotype res):
{
  Token end;
}
{
[ LOOKAHEAD(1)
  { boolean maybe = false; }
  ( end="[]" | end="[?]" { maybe = true; } )
  // parse recursively until then end of array qualifiers
  res = arrayMonotype(res)
  { 
    List tp = new LinkedList();
    tp.add(res);
    Location loc = res.location().englobe(new Location(end));
    res = new MonotypeConstructor
      (new TypeIdent(new LocatedString("Array", loc)),
       new TypeParameters(tp),
       loc);
    res.nullness = maybe ? res.maybe : res.absent;
  }
]

  { return res; }
  
}

Monotype simpleMonotype():
{
  Monotype res;
  List p=null;
  Token start, end;
  boolean maybe = false, sure = false;
}
{
  [ "?" { maybe = true; } | "!" { sure = true; } ]

  (
  res=funOrTupleMonotype()
|
  start="alike" [ "<" p=monotypes() ">" ]
  { res=new Alike(p, new Location(start)); }
  { res.nullness = res.absent; }
|
  LOOKAHEAD(monotypeConstructor())
  res=monotypeConstructor()
  { res.nullness = res.absent; }
| 
  res=typeIdent() 
  { res.nullness = res.absent; }
  )

  { 
    if ((maybe || sure) && 
        (res.nullness != res.absent))
      throw bossa.util.User.error
        (res, "Nullness is specified in two ways for " + res);
    if (maybe)
      res.nullness = res.maybe;
    else if (sure)
      res.nullness = res.sure;
  }

  { return res; }
}

List monotypes():
{
  List res=new ArrayList();
  Monotype t;
}
{
  t=monotype() { res.add(t); }
  ( "," t=monotype() { res.add(t); } )*
  { return res; }
}

List namedMonotypes():
{
  List res=new ArrayList();
  Monotype t;
}
{
  t=monotype() [ident()] { res.add(t); }
  ( "," t=monotype() [ident()] { res.add(t); } )*
  { return res; }
}

/***********************************************************************/
/*                          Classes                                    */
/***********************************************************************/

FormalParameters.Parameter formalParameter():
{
  Monotype t;
  LocatedString id;
  Expression val;
}
{
  t = monotype() 
  [ 
    id = ident() 
    [ 
      "=" val = Expression() 
      { return new FormalParameters.OptionalParameter(t, id, val); }
    ]
    { return new FormalParameters.NamedParameter(t, id); }
  ]
  { return new FormalParameters.Parameter(t); }
}

FormalParameters formalParameters(boolean leaveRoomForThis):
{
  List params = null;
  FormalParameters.Parameter p;
}
{
  { 
    if (leaveRoomForThis) 
    {
      params = new LinkedList();
      params.add(null);
    }
  }
  [
    p = formalParameter() 
    { if (params ==null) params = new LinkedList(); 
      params.add(p); }
    ( "," p = formalParameter() { params.add(p); } )*
  ]
  { return new FormalParameters(params); }
}

MethodDeclaration internalMethodOrFunction(MethodContainer c):
{
  LocatedString name;
  FormalParameters parameters;
  Constraint cst;
  Monotype returnType;
  Statement body = null;
  Contract contract;
}
{
  cst=constraint()
  returnType=monotype()
  name=identOrBackquoted()
  "(" parameters = formalParameters(true) ")"
  contract = contract()
  ( ";" | body = code() )
  
  { return NiceMethod.create(c, name, cst, returnType, parameters, 
                             body, contract); }
}

TypeIdent superClass():
{ TypeIdent name; }
{
  name=typeIdent() 
    // ignore type parameters
    // they should be checked
    [ "<" ident() ( "," ident() )* ">" ]
  { return name; }
}

List extensions():
{
  List res=new LinkedList();
  TypeIdent name;
}
{
  "extends" 
        name=superClass() { res.add(name); }
  ( "," name=superClass() { res.add(name); } )*
  { return res; }
}

List interfaces():
{
  List res=new LinkedList();
  TypeIdent name;
}
{
        name=superClass() { res.add(name); }
  ( "," name=superClass() { res.add(name); } )*
  { return res; }
}

void getField(NiceClass c, List fields):
{
  MonoSymbol field;
  boolean isFinal=false, isTransient = false, isVolatile = false;
  Expression value = null;
}
{
  ( 
    "public-read" [ "private-write" ]
  | "private-write"
  | ( ("public" | "private" | {} ) 
    [ ( "final" | "const" ) { isFinal=true; } ]
    )
  ) 
  [ "transient" { isTransient = true; } ]
  [ "volatile"  { isVolatile  = true; } ]
  field=monoSymbol() 
  [ "=" value=Expression() ]
  ";" 
  { fields.add(c.makeField(field, value, isFinal, isTransient, isVolatile)); }
}

void classDefinitionLookahead():
{}
{ [ "public" | "private" ] 
  ( "interface" | [ "final" | "abstract" ] "class") 
}

ClassDefinition classDefinition():
{
  ClassDefinition res;
  LocatedString name;
  MethodContainer.Constraint parameters;
  List fields=new ArrayList(),methods=null;
  List l;
  List typeParametersVariances = new ArrayList(0);
  boolean isFinal=false, isAbstract=false, isInterface=false;
}
{
  [ "public" | "private" ]
(
  "interface"
  name=ident() 
  parameters=classTypeParameters(typeParametersVariances) 
  { List ext = null; }
  [ ext = extensions() ]
  { res = ClassDefinition.makeInterface
      (name, parameters, typeParametersVariances, ext); }
|
  [ "final"    { isFinal=true; } 
  | "abstract" { isAbstract=true; }
  ]
  "class"
  name=ident() 
  parameters=classTypeParameters(typeParametersVariances) 

  { TypeIdent sup = null; List imp = null, abs = null; }

  [ "extends" sup = superClass() ]
  [ "implements" imp=interfaces() ]
  [ "finally" "implements"  abs=interfaces() ]
  { res = ClassDefinition.makeClass
	(name,
	isFinal,isAbstract,
	parameters,typeParametersVariances,
	sup,imp,abs);
  }
)

  ( 
   "{"
   { NiceClass impl = new NiceClass(res); }
   // internal fields and methods
   (
      LOOKAHEAD( getField(null, null) )
      getField(impl, fields)
    |
      [ "public" | "private" ]
      (
	LOOKAHEAD( methodBodyDefinitionLookahead() )
	{ MethodBodyDefinition mb; }
	mb = methodBodyDefinition(impl)
	{ globalDefinitions.add(mb); }
      |
        { MethodDeclaration m; }
        m = internalMethodOrFunction(res)
        { 
          if (methods == null)
            methods = new ArrayList(5);
          methods.add(m);
	  //globalDefinitions.add(m);
        }
      )
   )* 
   "}"
   {
     impl.setFieldsAndMethods(fields, methods);
     res.setImplementation(impl);
   }
  | 
   "=" "native"
   ( { LocatedString javaName; }
     javaName=doted_string()
     { res.setImplementation(new JavaClass(res, javaName)); }
   |
     { res.setImplementation(new PrimitiveType(res)); }
   )
   ";"
  )
  { return res; } 
}

AbstractInterface interfaceDefinition():
{
  LocatedString name;
  MethodContainer.Constraint parameters;
  List extensions=null;
  List typeParametersVariances = new ArrayList(0);
  AbstractInterface res;
}
{
  "abstract" "interface"
  name=ident()
  parameters=classTypeParameters(typeParametersVariances)
  [ "extends" extensions=interfaces() ]
  "{"
  { res = new AbstractInterface(name,parameters,typeParametersVariances,
				extensions); }
    (
      { MethodDeclaration m; }
      [ "public" | "private" ]
      m = internalMethodOrFunction(res)
      { res.addMethod(m); }
    )*
  "}"
  { return res; }
}

AbstractInterfaceImplementation abstractInterfaceImplementation():
{
  LocatedString className, interfaceName;
  boolean _finally = false;
}
{
  "class" className=fullyQualifiedName()
  [ "finally" { _finally = true; } ]
  "implements" interfaceName=ident()
  ";"
  { return new AbstractInterfaceImplementation(className, interfaceName, _finally); }
}


MethodContainer.Constraint classTypeParameters(List tags):
{}
{
  [
    {
      List binders = new ArrayList(3);
      List atoms = new ArrayList(3);
      MonotypeVar var;
    }

    "<" 
    var=taggedMonotypeVar(tags, atoms) { binders.add(var); }
    ( "," var=taggedMonotypeVar(tags, atoms) { binders.add(var); } )*
    [ "|" atomics(atoms) ]
    ">" 
    { 
      MonotypeVar[] b = (MonotypeVar[]) binders.toArray(new MonotypeVar[binders.size()]);
      return new MethodContainer.Constraint(b, atoms); 
    }
  ]
  { return null; }
}

MonotypeVar taggedMonotypeVar(List tags, List atoms):
{
  MonotypeVar mv;
  Monotype bound = null;
  boolean sure = false;
}
{
  [ LOOKAHEAD( monotype() ("+" | "-" | monotypeVar()) )
    bound=monotype() 
  ]

  ( "+" { tags.add(Boolean.TRUE); }
  | "-" { tags.add(Boolean.FALSE); }
  |     { tags.add(null); }
  )

  [ "!" { sure = true; } ]

  mv = monotypeVar()

  { 
    if (bound != null)
      {
        bound.nullness = Monotype.absent;
        atoms.add(new MonotypeLeqCst(Monotype.createVar(mv), bound)); 
      }

    if (sure)
      atoms.add(AtomicConstraint.sureTypeVar(mv));

    return mv;
  }
}

/***********************************************************************/
/*                          Methods                                    */
/***********************************************************************/

Pattern pattern():
{
  LocatedString name = null;
  Token t;
  TypeIdent tc=null, additional = null;
  Monotype type=null;
  Expression val = null;
  boolean exactlyAt = false;
  Location loc = null;
}
{
  ( 
    val=patternLiteral() { loc = val.location(); }
  |
    (
      t="@"
    |
      t="#" { exactlyAt = true;}
    )
    (
      tc=typeIdent() { loc = tc.location(); }
    |
      //temporarily added to warn the user about the deprecated '@null' syntax.
      t="null" 
      { val = NullExp.instance;
        loc = new Location(t);
        User.warning(loc,"The '@null' syntax is deprecated, leave the '@' away.");
      }
    )
  |
    name = ident()
    (
      t ="@"
      (
        val=patternLiteral()
      |
        tc=typeIdent()
      )
    |
      t ="#" tc=typeIdent() { exactlyAt = true; }
    |
     {} //name only
    )
    { loc = name.location(); }
  )
  [ "(" additional=typeIdent() ")" ]
  [ ":" type=monotype() ]
  {
    return new Pattern(name, tc, val, exactlyAt, additional, type, loc);
  }
}

void methodBodyDefinitionLookahead():
{}
{
  identOrBackquoted() [ "<" strings() ">" ] "("
}

Statement code():
{
  Statement code;
}
{
  ( 
    // A true body
    code=Block()
  |
    // An expression
    { Expression exp; }
    "=" 
    ( "." "." "." { return null; } // toplevel function in interface file
    |
    exp=Expression() ";"
    { 
      code = new ReturnStmt(exp, /* fake */ true);
    }
    )
  )
  { return code; }
}

MethodBodyDefinition methodBodyDefinition(NiceClass container):
{
    LocatedString name;
    Pattern p;
    Collection binders=null;
    //List newTypeVars=null;
    LinkedList parameters=new LinkedList();
    Statement body;
    Expression exp;
}
{
    name=identOrBackquoted()
    [ "<" binders=strings() ">" ]

// introducing new Type variables
//    [ "[" newTypeVars=typeConstructors() "]" ]

    "("
    ( 
	p=pattern() { parameters.add(p); }
	( "," p=pattern() { parameters.add(p); } )*
    )?
    ")"
    body = code()
    { return new MethodBodyDefinition(container,name,binders,
				      //newTypeVars,
				      parameters,body); }
}

MethodDeclaration methodDeclaration():
{
    LocatedString name;
    Monotype returnType;
    FormalParameters parameters;
    Constraint cst;
    MethodDeclaration res;
}
{
  [ "public" | "private" ]
  cst=constraint()
    returnType=monotype()
    name=identOrBackquoted()
    "(" parameters = formalParameters(false) ")"
    
    ( LOOKAHEAD(2)
      "=" 
      (
        "native" res=nativeMethodDeclaration(name,cst,returnType,parameters)
      |
	"inline" 
	{ LocatedString inlinedName; String param = null; }
	inlinedName = doted_string() 
	[
	  "(" [ param=rawString() ] ")"
	]
	";"
	{ res=new InlinedMethod(name,cst,returnType,parameters,
				inlinedName, param); }
      )
    | { Contract contract; }
      contract = contract()
      (
        { Statement body; }
        body = code()
        { res = new ToplevelFunction(name, cst, returnType, parameters, 
                                     body, contract);}
      |
        // Nice method
        ";"
        { res = new NiceMethod(name,cst,returnType,parameters,contract); }
      )
    )
  { return res; }
}

// Designing a java method, found thru reflection
MethodDeclaration nativeMethodDeclaration(LocatedString name,
					Constraint cst,
					Monotype returnType,
					FormalParameters parameters):
{
  List javaTypes=null;
  LocatedString method,s=null;
  boolean isNew=false;
}
{
(
  "new" { isNew = true; }
|
  s=doted_array_string() 
)

// if we stop here with a ";", it's a static field
// else it's a method
( 
  ";"
  { method=s; }
| 
  { 
    javaTypes=new ArrayList();
     if(!isNew) javaTypes.add(s);
  }

  method=doted_string()
  "("
  [
    s=doted_array_string() { javaTypes.add(s); }
    ( "," s=doted_array_string() { javaTypes.add(s); } )*
  ]
  ")"  
  ";"
)

  { 
    if(isNew)
      {
	javaTypes.add(0,method);
	return new RetypedJavaMethod(method,"<init>",javaTypes,
			             name,cst,returnType,parameters);
      }
    int dot=method.toString().lastIndexOf('.');
    if(dot==-1)
      throw bossa.util.User.error(method, "Class or method name missing");
    
    String methodName=method.toString();
    methodName = methodName.substring(dot+1,methodName.length());
    LocatedString className=method.substring(0,dot);
    
    if(javaTypes!=null)
      return new RetypedJavaMethod(className,methodName,javaTypes,
			           name,cst,returnType,parameters);
    else
      return new JavaFieldAccess(className,methodName,
				 name,cst,returnType,parameters);
  }
}

Definition globalVariable():
{
  LocatedString name;
  Monotype t;
  Expression value=null;
}
{
  "var"
  t=monotype()
  name=ident()
  [ "=" value=Expression() ]
  ";"
  { return new GlobalVarDeclaration(name,t,value); }
}

Contract contract() :
{
  Contract res = Contract.noContract;
  Expression condition, name;
}
{
  [ "requires" 
    { res = new Contract(); }

      condition = SideEffectFreeExpression() 
      ( ":" name = SideEffectFreeExpression() 
        { res.addRequire(condition, name); }
      | { res.addRequire(condition); } )

    ( "," [ // Trailing "," allowed for easy reordering, removal of last line..
      condition = SideEffectFreeExpression() 
      ( ":" name = SideEffectFreeExpression() 
        { res.addRequire(condition, name); }
      | { res.addRequire(condition); } )
    ] )*
  ]
  [ "ensures" { if (res == Contract.noContract) res = new Contract(); }

      condition = SideEffectFreeExpression() 
      ( ":" name = SideEffectFreeExpression() 
        { res.addEnsure(condition, name); }
      | { res.addEnsure(condition); } )

    ( "," [ // Trailing "," allowed for easy reordering, removal of last line..
      condition = SideEffectFreeExpression() 
      ( ":" name = SideEffectFreeExpression() 
        { res.addEnsure(condition, name); }
      | { res.addEnsure(condition); } )
    ] )*
  ]
  { return res; }
}

Definition definition():
{
    Definition res;
}
{
  (
    res=globalVariable()
  | 
    LOOKAHEAD ( "abstract" "interface" )
    res=interfaceDefinition()
  |
    LOOKAHEAD( "class" fullyQualifiedName() )
    res=abstractInterfaceImplementation()
  | 
    LOOKAHEAD ( classDefinitionLookahead() )
    res=classDefinition()
  | 
    LOOKAHEAD( methodBodyDefinitionLookahead() )
    res=methodBodyDefinition(null)
  |
    res=methodDeclaration()
  )
  { return res; }
}

void readDefinitions(List definitions):
{
    Definition d;
    globalDefinitions = definitions;
}
{
  ( 
   d = definition() { definitions.add(d); } 
  )*
  <EOF> 
}

/***********************************************************************/
/*                          Expressions                                */
/***********************************************************************/

Expression intConstantExp():
{
  LocatedString representation;
}
{
  representation=integerLiteral()
  { return ConstantExp.makeNumber(representation); }
}

Expression floatConstantExp():
{
  LocatedString representation;
}
{
  representation=floatingLiteral()
  { return ConstantExp.makeFloating(representation); }
}

Expression charConstantExp():
{
  Token t;
}
{
  t = <CHARACTER_LITERAL> 
  { return ConstantExp.makeChar
      (new LocatedString(t.image.substring(1, t.image.length() - 1),
			 new Location(t)));
  }
}

StringConstantExp stringConstantExp():
{
  String s;
}
{
  s=rawString()
  { return new StringConstantExp(s); }
}

IdentExp identExp():
{
  LocatedString i;
}
{
  i=identOrBackquoted()
  { return new IdentExp(i); }
}

Expression newExp():
{
  Token t;
  TypeIdent classe;
  Expression e;
}
{
  t="new" 
  classe=typeConstructorIdent()
  ( 
    { Arguments arguments; }
    arguments = arguments(false)
    { e = new NewExp(classe, arguments); 
      e.setLocation(new Location(t));
      return e;
    }
  |
    { List args = new LinkedList(); int furtherDimensions = 0; }
    ( LOOKAHEAD(2) "[" e=Expression() "]" { args.add(e); } )+ 
    ( LOOKAHEAD(2) "[]" { furtherDimensions++; } )*
    { return new NewArrayExp(classe, args, furtherDimensions); }
  )
}

FunExp funExp():
{
  List formals;
  Constraint cst;
  Token f = null;
  FunExp res;
}
{
  [f = "fun"] 
  cst=constraint()
  "(" formals=monoSymbols() ")"
  res=endFunExp(formals, cst)
  {
    if (f != null) 
      bossa.util.User.warning(new Location(f), "The 'fun(...) =>' syntax is deprecated, just leave 'fun' away."); 
    return res;
  }
}

FunExp simpleVarFunExp():
{
  MonoSymbol sym;
  List formals;
  FunExp res;
}
{
  sym=monoSymbol() 
  { formals = new LinkedList(); formals.add(sym); }
  res=endFunExp(formals, Constraint.True)
  { return res; }
}

FunExp endFunExp(List formals, Constraint cst):
{
  Statement body;
  Expression exp;
}
{
  "=>"
  (
    body = RootShortStatement()
  |
    exp=Expression()
    { 
      body=new ReturnStmt(exp);
    }
  )
  { return new FunExp(cst,formals,body); }
}

/** A statement not terminated by a ";" if applicable. */
Statement RootShortStatement() :
{
  Statement res;
}
{
  (
  res = Block()
|
  res = ShortIfStatement()
|
  res = ShortAssertStatement()
|
  res = ShortThrowStatement()
  )
  { return res; }
}

Statement ShortStatement() :
{
  Statement res;
}
{
  (
  res = RootShortStatement()
|
  res = StatementExpression()
  )
  { return res; }
}

// ( e1, ..., en )
Expression tupleExp():
{
  List exps;
  Expression e;
}
{
  // A tuple must have at least two elements (thus a ",")
  // which avoids ambiguity with expression grouping thru "(" ")"

  "(" { exps = new LinkedList(); }
  e=Expression() 

  // either we have another expression ans it's a real tuple
  // either we close the parenthesis and it was grouping
  (
    ")" { return e; }
  |
    { exps.add(e); }
    "," e=Expression() { exps.add(e); }
    (
      "," e=Expression() { exps.add(e); }
    )*
    ")"
    { return new TupleExp(exps); }
  )
}

// [ e1, ..., en ]
Expression arrayLiteralExp():
{
  List exps;
  Expression e;
}
{
  "[]" { return new LiteralArrayExp(null); }
|
  "[" { exps = new LinkedList(); }
  
  [
    e=Expression() { exps.add(e); }
    (
      "," e=Expression() { exps.add(e); }
    )*
  ]

  "]"
  { return new LiteralArrayExp(exps); }
}

/****************************************************************
 * Imported Packages
 ****************************************************************/

LocatedString readImports(List imports, Collection opens):
{
  LocatedString pkgName=null, name;
}
{
  [ "package" pkgName=doted_string() ";" ]

  (
   "import" name=doted_string() 

   ( ".*" { opens.add(name.toString()); }
   |      { imports.add(name); }
   )
   ";"
  )*
  { return pkgName; }
}

void readOpens():
{
}
{
  [ "package" doted_string() ";" ]

  (
   "import" doted_string() [ ".*" ]
   ";"
  )*
}

/****************************************************************
 * Modules
 ****************************************************************/

void module(List definitions):
{}
{
  readOpens()
  readDefinitions(definitions)
}

/****************************************************************
 * JAVA statements and expressions
 ****************************************************************/

// Java 1.1 syntax-compliant 
// Derived from the Java1.1.jj file that comes with JavaCC

/*
 * Expression syntax follows.
 */

Expression Expression() :
/*
 * This expansion has been written this way instead of:
 *   Assignment() | ConditionalExpression()
 * for performance reasons.
 * However, it is a weakening of the grammar for it allows the LHS of
 * assignments to be any conditional expression whereas it can only be
 * a primary expression.  Consider adding a semantic predicate to work
 * around this.
 */
{ Expression e1,e2; Token op; Token first,last; }
{
  { first=getToken(1); }

  ( //TODO: simplify next lookahead after removal of the 'fun' keyword.
    LOOKAHEAD(funExp()) e1=funExp() 
  | LOOKAHEAD(monoSymbol() "=>") e1=simpleVarFunExp() 
  | e1=ClassicExpression() 
  )
  { 
    last=getToken(0);
    e1.setLocation(new Location(first, last));
    return e1;
  }
}

Expression ClassicExpression() :
{ Expression e1,e2; Token op, start = getToken(1); }
{
  e1=ConditionalExpression()
  [ 
    { e1.setLocation(new Location(start, getToken(0))); }
    op=AssignmentOperator() 
    e2=Expression()
    {
      if(op.image.length()!=1)
        e2=CallExp.create(symb(op.image.substring(0, 1),op),e1,e2);

      e1=AssignExp.create(e1,e2);
    }
  ]
  { return e1; }
}

Expression SideEffectFreeExpression() :
{ Expression e; }
{
  e = ConditionalExpression()
  { return e; }
}

Token AssignmentOperator() :
{ Token res; }
{
  (
    res = "="
  |
    res= < assignmentOperator : 
      "*=" | "/=" | "%=" | "+=" | "-=" | 
      "<<=" | ">>=" | ">>>=" | "&=" | "^=" | "|=" 
      >
  )
  { return res; }
}

Expression ConditionalExpression() :
{ Expression res,e1,e2; Token start; }
{
  res=ConditionalOrExpression() 
    [
      "?" e1=Expression() start=":" e2=ConditionalExpression() 
      { e2.setLocation(new Location(start, getToken(0))); 
        res=new IfExp(res,e1,e2); }
    ]
  { return res; }
}

Expression ConditionalOrExpression() :
{ Expression e1,e2; Token t; }
{
  e1=ConditionalAndExpression() 
    ( t="||" e2=ConditionalOrExpression() 
      { e1=CallExp.create(symb(t), e1, e2); } )?
  { return e1; }
}

Expression ConditionalAndExpression() :
{ Expression e1,e2; Token t; }
{
  e1=InclusiveOrExpression() 
    ( t="&&" e2=ConditionalAndExpression()
      { e1=CallExp.create(symb(t), e1, e2); } )?
  { return e1; }
}

Expression InclusiveOrExpression() :
{ Expression e1,e2; Token t; }
{
  e1=ExclusiveOrExpression() 
    ( t="|" e2=ExclusiveOrExpression() 
      { e1=CallExp.create(symb(t),e1,e2); } )*
  { return e1; }
}

Expression ExclusiveOrExpression() :
{ Expression e1,e2; Token t; }
{
  e1=AndExpression() 
    ( t="^" e2=AndExpression() 
      { e1=CallExp.create(symb(t),e1,e2); } )*
  { return e1; }
}

Expression AndExpression() :
{ Expression e1,e2; Token t; }
{
  e1=EqualityExpression() 
    ( t="&" e2=EqualityExpression() 
      { e1=CallExp.create(symb(t),e1,e2); } )*
  { return e1; }
}

Expression EqualityExpression() :
{ Expression e1,e2; Token t; }
{
  e1=InstanceOfExpression() 
    ( ( t="==" | t="!=" ) e2=InstanceOfExpression() 
      { e1=CallExp.create(symb(t),e1,e2); }
      )*
  { return e1; }
}

Expression InstanceOfExpression() :
{ Expression res; }
{
  res=RelationalExpression() 
  [ { Token t; ConstantExp type; } 
    t="instanceof" type=typeExpression() 
    { return CallExp.create(symb(t), res, type); }
  ]
  { return res; }
}

Expression RelationalExpression() :
{ Expression e1, e2, e3; Token t; e3 = null; }
{
  e1=ShiftExpression()
    [ (
	( (t=">"|t=">=") e2=ShiftExpression()
	  {
            if(e3 == null) e1=CallExp.create(symb(t),e1,e2);
            else e1=CallExp.create(symb("&&",t),e1,CallExp.create(symb(t),e3,e2));
            e3 = e2;
          }
        )+
      |
	( (t="<"|t="<=") e2=ShiftExpression()
	  {
            if(e3 == null) e1=CallExp.create(symb(t),e1,e2);
            else e1=CallExp.create(symb("&&",t),e1,CallExp.create(symb(t),e3,e2));
            e3 = e2;
          }
        )+
      )
    ]
  { return e1; }
}

Expression ShiftExpression() :
{ Expression e1,e2; Token t,t1=null; boolean left=false; }
{
  e1=AdditiveExpression() 
    ( LOOKAHEAD(2)
     ( t="<" "<" { left=true; }
     | t=">" ">" [ t1=">" ]
     )
     e2=AdditiveExpression() 
     { 
       String func;
       if(left)
         func = "<<";
       else
         func = t1==null ? ">>" : ">>>";
       e1=CallExp.create(symb(func,t),e1,e2); }
      )*
  { return e1; }
}

Expression AdditiveExpression() :
{ Expression e1,e2; Token t; }
{
  e1=MultiplicativeExpression() 
    ( ( t="+" | t="-" ) e2=MultiplicativeExpression() 
      { e1=CallExp.create(symb(t),e1,e2); }
      )*
  { return e1; }
}

Expression MultiplicativeExpression() :
{ Expression e1,e2; Token t; }
{
  e1=UnaryExpression() 
    ( (t="*"|t="/"|t="%") e2=UnaryExpression() 
      { e1=CallExp.create(symb(t),e1,e2); }
    )*
  { return e1; }
}

Expression UnaryExpression() :
{ Expression res; Token t=null; }
{
  (
  ( t="+" | t="-" ) res=PrimaryExpression() 
    { res=CallExp.create(symb(t),res); }
|
  res=PreIncrementExpression()
|
  res=PreDecrementExpression()
|
  res=UnaryExpressionNotPlusMinus()
  )
  { return res; }
}

Expression PreIncrementExpression() :
{ Expression res; Token t; }
{
  t="++" res=PrimaryExpression()
  { return new IncrementExp(res, true, true); }
}

Expression PreDecrementExpression() :
{ Expression res; Token t; }
{
  t="--" res=PrimaryExpression()
  { return new IncrementExp(res, true, false); }
}

Expression UnaryExpressionNotPlusMinus() :
{ Expression res; Token t; }
{
  ( t="~" | t="!" ) res=PrimaryExpression()
  { return CallExp.create(symb(t),res); }

/*
|
  LOOKAHEAD( CastLookahead() )
  CastExpression()
*/

|
  res=PostfixExpression()
  { return res; }
}

/*
// This production is to determine lookahead only.  The LOOKAHEAD specifications
// below are not used, but they are there just to indicate that we know about
// this.
Expression CastLookahead() :
{ Expression res; }
{
  LOOKAHEAD(2)
  "(" PrimitiveType()
|
  LOOKAHEAD("(" Name() "[")
  "(" Name() "[" "]"
|
  "(" Name() ")" ( "~" | "!" | "(" | <IDENTIFIER> | "this" | "super" | "new" | Literal() )
}
*/

Expression PostfixExpression() :
{ Expression res; Token t; }
{
  res=PrimaryExpression() 
  [ "++" { res=new IncrementExp(res, false, true); }
  | "--" { res=new IncrementExp(res, false, false); }
  ] 
  { return res; }
}

/*
Expression CastExpression() :
{ Expression res; }
{
  LOOKAHEAD("(" PrimitiveType())
  "(" Type() ")" UnaryExpression()
|
  LOOKAHEAD("(" Name())
  "(" Type() ")" UnaryExpressionNotPlusMinus()
}
*/

Expression PrimaryExpression() :
{ Expression res; }
{
  res=PrimaryPrefix() 
    ( LOOKAHEAD(2) res=PrimarySuffix(res) )*
  { return res; }
}

Expression PrimaryPrefix() :
{ Expression res; }
{
(
  res=Literal()

/*
|
  "this"
*/

|
  "super" 
  { res = new SuperExp(); }

|
  // This includes the case "(" Expression() ")" to avoid lookahead
  res=tupleExp()
|
  res=arrayLiteralExp() 
|
  //AllocationExpression()
  res=newExp()

|
  LOOKAHEAD( doted_array_string() "." "class" )
  { ConstantExp type; Token t; }
  type=typeExpression() "." t="class"
  { return CallExp.create(symb(t), type); }
|
  //Name()
  res=identExp()

)
  { return res; }
}

Expression PrimarySuffix(Expression start) :
{ 
  Expression res;
  Arguments args = null;
}
{

/*
  LOOKAHEAD(2)
  "." "this"
|
  LOOKAHEAD(2)
  "." AllocationExpression()
|
*/

  { Token t; }
  t="[" res=Expression() "]"
  { return CallExp.create(symb("get",t,false),start,res); }

|
  "." res=identExp()
  [ LOOKAHEAD(1) // a.f(x) is parsed as f(a, x), not as (f(a))(x)
    args = arguments(true) ]
  { 
    boolean hasBrackets = args != null;

    if (args == null)
      {
	List l = new LinkedList();
	l.add(new Arguments.Argument(start));
	args = new Arguments(l);
      }
    else
      args.addReceiver(start);
    return new CallExp(res, args, true, hasBrackets); 
  }

|
  args = arguments(false)
  { return new CallExp(start, args); }
}

Expression Literal() :
{
  Expression res;
  Token t;
}
{
  (
  "null" { res = NullExp.instance; }
| res=intConstantExp() 
| res=floatConstantExp()
| res=charConstantExp()
| res=stringConstantExp()
  )
{ return res; }
}

Expression patternLiteral() :
//restricted literal for use in dispatch patterns
{
  Expression res;
  Token t;
}
{
  (
  "null" { res = NullExp.instance; }
| "-" res=intConstantExp() 
  { res = ConstantExp.makeNumber(
         new LocatedString("-"+res.toString(), res.location()));
  }
| res=intConstantExp() 
| res=charConstantExp()
  )
{ return res; }
}

Arguments.Argument argument():
{
  Expression e;
  LocatedString id = null;
}
{
  [ LOOKAHEAD(ident() ":") id = ident() ":" ]
  e = Expression()
  { return new Arguments.Argument(e, id); }
}

Arguments arguments(boolean leaveRoomForReceiver):
{
  List res = new ArrayList();
  Arguments.Argument a;
}
{
  "("
  { if (leaveRoomForReceiver) res.add(null); }
  [
    a = argument() { res.add(a); }
    ( "," a = argument() { res.add(a); } )*
  ]
  ")"
  { return new Arguments(res); }
}

/*
void AllocationExpression() :
{}
{
  LOOKAHEAD(2)
  "new" PrimitiveType() ArrayDimsAndInits()
|
  "new" Name()
    (
      ArrayDimsAndInits()
    |
      Arguments() [ ClassBody() ]
    )
}
*/

/*
 * The second LOOKAHEAD specification below is to parse to PrimarySuffix
 * if there is an expression between the "[...]".
 */
/*
void ArrayDimsAndInits() :
{}
{
  LOOKAHEAD(2)
  ( LOOKAHEAD(2) "[" Expression() "]" )+ ( LOOKAHEAD(2) "[" "]" )*
|
  ( "[" "]" )+ ArrayInitializer()
}
*/




/*
 * Statement syntax follows.
 */

Statement Statement() :
{ Token first, last; Statement res; }
{
  { first = getToken(1); }
  (
  LOOKAHEAD(2)
  res=LabeledStatement()
|
  res=Block()
|
  res=EmptyStatement()
|
  res=StatementExpression() ";"

/*
|
  res=SwitchStatement()
*/

|
  res=IfStatement()
|
  res=WhileStatement()
|
  res=DoStatement()
|
  res=ForStatement()
|
  res=BreakStatement()
|
  res=ContinueStatement()
|
  res=ReturnStatement()
|
  res=ThrowStatement()
|
  res=TryStatement()
|
  res=AssertStatement()

|
  res=SynchronizedStatement()
  )
  { 
    if (res != null)
      {
        last = getToken(0);
        res.setLocation(new Location(first, last));
      }
    return res; 
  }
}

Statement LabeledStatement() :
{ LocatedString label; Statement res; boolean forLoop; }
{
  label=ident() ":" 
  { forLoop = "for".equals(getToken(1).toString()); }
  res=Statement()
  { 
    // Fix labeling of for loops:
    // for(int i, ...) is parsed as a block defining a local variable i
    // however we want the label to target the loop so that continue works
    if (forLoop && !(res instanceof LoopStmt))
      return new LabeledStmt(label, res, (LoopStmt) ((Block) res).last());
    else
      return new LabeledStmt(label, res); 
  }
}

Statement Block() :
{ Token first, last; Statement s; List statements; }
{
  { first = getToken(1); }
  "{" 
  { statements = new LinkedList(); }
  ( s=BlockStatement(statements) { statements.add(s); } )* 
  "}"
  { 
    last = getToken(0);
    Statement res = new Block(statements); 
    res.setLocation(new Location(first, last));
    return res;
  }
}

Statement BlockStatement(List statements) :
{ Statement res; }
{
  (
  LOOKAHEAD( "final" | "const" | monotype() <IDENT>)
  (
    LOOKAHEAD( ( "final" | "const" ) <IDENT> "=" )
    res = LocalConstant()
  |
    res = LocalDeclaration() 
  )
|
  // There are two cases for tuples (declaring a variable or not). 
  // Since it seems we cannot have a lookahead inside another we 
  // repeat two instances with different lookaheads.
  LOOKAHEAD( "(" monotype() ident() "," )
  res=LocalTupleDeclaration(statements)
| 
  // Second case (see above).
  LOOKAHEAD( "(" ident() "," )
  res=LocalTupleDeclaration(statements)
|
  res=Statement()
/*
|
  UnmodifiedClassDeclaration()
*/
  )
  { return res; }
}

Statement LocalDeclaration() :
{
  Monotype t = null;
  LocatedString id;
  FormalParameters parameters;
  Statement body;
  Expression e=null;
  boolean constant = false;
}
{
  [ ( "final" | "const" ) { constant = true; } ]
  t = monotype()
  id=ident()
  (
    "(" parameters = formalParameters(false) ")" body = code() 
    { return Block.LocalFunction.make(id, t, parameters, body); }
  | 
    [ "=" e=Expression() ] 
    { Block.LocalVariable res = new Block.LocalVariable(id,t,constant,e); }
    ( "," id=ident() {e=null;} [ "=" e=Expression() ] { res.addNext(id,e); } )*
    ";" 
    { return res; }
  )
}

Statement LocalConstant() :
{
  LocatedString id;
  FormalParameters parameters;
  Statement body;
  Expression e=null;
  boolean constant = false;
}
{
  ( "final" | "const" )
  id=ident()
  "=" 
  e=Expression()
  { Block.LocalConstant res = new Block.LocalConstant(id,e); }
  ( "," id=ident() "=" e=Expression() { res.addNext(id,e); } )*
  ";" 
  { return res; }
}

Statement LocalTupleDeclaration(List statements) :
{
  Expression e;
}
{
  "(" { List parts = new LinkedList(); Expression part; }
  part = LocalTuplePart(statements) { parts.add(part); }
  ( "," part = LocalTuplePart(statements) { parts.add(part); } )+
  ")"
  "=" e=Expression() ";" 
  { return new ExpressionStmt(AssignExp.create(new TupleExp(parts), e)); }
}

Expression LocalTuplePart(List statements) :
{}
{
  LOOKAHEAD( "(" LocalTuplePart() "," )
  "(" { List parts = new LinkedList(); Expression part; }
  part = LocalTuplePart(statements) { parts.add(part); }
  ( "," part = LocalTuplePart(statements) { parts.add(part); } )+
  ")"
  { return new TupleExp(parts); }
|
  { LocatedString id; Monotype type = null; }
  [ LOOKAHEAD(monotype() ident()) type = monotype() ] id = ident() 
  { 
    if (type != null)
      statements.add(new Block.LocalVariable(id, type, false, null)); 
    return new IdentExp(id);
  }
}

Statement EmptyStatement() :
{}
{
  ";" { return null; }
}

Statement StatementExpression() :
/*
 * The last expansion of this production accepts more than the legal
 * Java expansions for StatementExpression.  This expansion does not
 * use PostfixExpression for performance reasons.
 */
{ Expression e1; Token first, last; }
{
  { first = getToken(1); }
  (
  e1=PreIncrementExpression()
|
  e1=PreDecrementExpression()
|
  e1=PrimaryExpression()
  [
    "++" { e1=new IncrementExp(e1, false, true); }
  |
    "--" { e1=new IncrementExp(e1, false, false); }
  |
    { Token op; Expression e2; 
      e1.setLocation(new Location(first, getToken(0)));
    }
    op=AssignmentOperator() 
    e2=Expression()
    {
      if(op.image.length()!=1)
        e2=CallExp.create(symb(op.image.substring(0, 1),op),e1,e2);

      e1=AssignExp.create(e1,e2);
    }
  ]
  )
  {
    last = getToken(0);
    e1.setLocation(new Location(first, last));
    return new ExpressionStmt(e1); 
  }
}

/*
Statement SwitchStatement() :
{ Statement res; }
{
  "switch" "(" Expression() ")" "{"
    ( SwitchLabel() ( BlockStatement() )* )*
  "}"
}

void SwitchLabel() :
{
  "case" Expression() ":"
|
  "default" ":"
}
*/

Statement IfStatement() :
/*
 * The disambiguating algorithm of JavaCC automatically binds dangling
 * else's to the innermost if statement.  The LOOKAHEAD specification
 * is to tell JavaCC that we know what we are doing.
 */
{ Token first; Statement s1,s2=null; Expression cond; IfExp ifExp; }
{
  first="if" "(" cond=Expression() ")" 
    s1=Statement() [ LOOKAHEAD(1) "else" s2=Statement() ]
  { 
    ifExp = new IfExp(cond, new StatementExp(s1),
	              (s2 == null ? null : new StatementExp(s2)));
    ifExp.setLocation(new Location(first, getToken(0)));
    return new ExpressionStmt(ifExp);
  }
}

Statement ShortIfStatement() :
/*
 * The disambiguating algorithm of JavaCC automatically binds dangling
 * else's to the innermost if statement.  The LOOKAHEAD specification
 * is to tell JavaCC that we know what we are doing.
 */
{ Token first; Statement s1,s2=null; Expression cond; IfExp ifExp; }
{
  first="if" "(" cond=Expression() ")" 
    s1=ShortStatement() [ LOOKAHEAD(1) "else" s2=ShortStatement() ]
  { 
    ifExp = new IfExp(cond, new StatementExp(s1),
	              (s2 == null ? null : new StatementExp(s2)));
    ifExp.setLocation(new Location(first, getToken(0)));
    return new ExpressionStmt(ifExp);
  }
}

Statement WhileStatement() :
{ Expression cond; Statement body; }
{
  "while" "(" cond=Expression() ")" body=Statement()
  { return LoopStmt.whileLoop(cond,body); }
}

Statement DoStatement() :
{ Expression cond; Statement body; }
{
  "do" body=Statement() "while" "(" cond=Expression() ")" ";"
  { return LoopStmt.doLoop(cond,body); }
}

Statement ForStatement() :
{ Statement init=null,update=null,body,loop,block; Expression cond=null; }
{
  "for" "(" 
  (  
    LOOKAHEAD( monotype() <IDENT> ":" ) block=ForInStatement()
  |
    ( init=ForInit() | ";" )
    [ cond=Expression() ] ";" [ update=StatementExpressionList() ] ")" 
    body=Statement()
    { 
      loop=LoopStmt.forLoop(cond,update,body);
      if(init==null)
        return loop;
  
      List l = new LinkedList();
      l.add(init);
      l.add(loop);
      block = new Block(l);
    }
  )
  { return block; }
}

Statement ForInStatement() :                                                
{ Statement body; Monotype vartype; LocatedString var; Expression container; Token t; }
{
  vartype=monotype() var=ident() t=":" container=Expression() ")"
  body=Statement()
  {return LoopStmt.forInLoop(vartype,var,new Location(t),container,body); }
}

Statement ForInit() :
{ Statement res; }
{
  (
  LOOKAHEAD( [ "final" | "const" ] monotype() <IDENT> )
  res=LocalDeclaration()
|
  res=StatementExpressionList() ";"
  )
  { return res; }
}

Statement StatementExpressionList() :
{ Statement s; List statements=new LinkedList(); }
{
  s=StatementExpression() { statements.add(s); }
  ( "," StatementExpression() { statements.add(s); }
  )*
  { return new Block(statements); }
}

Statement BreakStatement() :
{ LocatedString label = null; }
{
  "break" [ label=ident() ] ";"
  { if (label == null) return BreakStmt.instance; 
    else return new BreakLabelStmt(label); 
  }
}

Statement ContinueStatement() :
{ Statement res; LocatedString label = null; }
{
  "continue" [ label=ident() ] ";"
  { return ContinueStmt.make(label); }
}

Statement ReturnStatement() :
{ Expression val=null; Token t; }
{
  t="return" [ val=Expression() ] ";"
  {
    Statement res = new ReturnStmt(val); 
    res.setLocation(new Location(t));
    return res;
  }
}

Statement ShortThrowStatement() :
{ Expression e; Token t; }
{
  t="throw" e=Expression()
  { return new ExpressionStmt(CallExp.create(symb(t),e)); }
}

Statement ThrowStatement() :
{ Statement res; }
{
  res = ShortThrowStatement() ";"
  { return res; }
}

Statement TryStatement() :
/*
 * Semantic check required here to make sure that at least one
 * finally/catch is present.
 */
{ TryStmt res; Statement body, b; 
  TypeIdent tc;
  LocatedString i;
}
{
  "try" body=Block()
  { res = new TryStmt(body); }
  ( "catch" "(" tc=typeConstructorIdent() i=ident() ")" b=Block() 
  { res.addCatch(tc, i, b); }
  )*
  [ "finally" b=Block() { res.setFinally(b); } ]
  { return res; }
}

Statement ShortAssertStatement() :
{
  Token t;
  Expression condition, message;
  CallExp call;
}
{
  t = "assert"
  condition = SideEffectFreeExpression()
  ( ":" message = SideEffectFreeExpression() 
    { call = CallExp.create(symb(t), condition, message); }
  |
    { call = CallExp.create(symb(t), condition); }
  )
  { return new ExpressionStmt(call); }
}

Statement AssertStatement() :
{ Statement res; }
{
  res = ShortAssertStatement()
  ";"
  { return res; }
}

Statement SynchronizedStatement() :
{ Expression object; Statement body; }
{
  "synchronized" "(" object=Expression() ")" body=Block()
  { return new SynchronizedStmt(object, body); }
}
