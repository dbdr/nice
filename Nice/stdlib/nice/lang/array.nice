/**************************************************************************/
/*                                N I C E                                 */
/*             A high-level object-oriented research language             */
/*                        (c) Daniel Bonniot 2001                         */
/*                                                                        */
/*  This program is free software; you can redistribute it and/or modify  */
/*  it under the terms of the GNU General Public License as published by  */
/*  the Free Software Foundation; either version 2 of the License, or     */
/*  (at your option) any later version.                                   */
/*                                                                        */
/**************************************************************************/

package nice.lang;

/**
   Support for native arrays.
   Make them elements of the collection hierarchy.

   T[] is an equivalent notation for Array<T>
*/

final class Array<T>
  implements Sequence<T>
  finally implements Collection<T>
  = native;

/** Returns a new copy with newSize elements */
<Any T> T[] resize(T[], int newSize) = native Object Native.resize(Object,int);

<Any T> int length(T[]) = inline nice.lang.inline.ArrayLength();

<Any T> T    get(T[], int)    = inline nice.lang.inline.ArrayGetOp("o");
<Any T> void set(T[], int, T) = inline nice.lang.inline.ArraySetOp("o");


boolean get(boolean[], int) = inline nice.lang.inline.ArrayGetOp("z");
byte    get(byte[],    int) = inline nice.lang.inline.ArrayGetOp("b");
short   get(short[],   int) = inline nice.lang.inline.ArrayGetOp("s");
int     get(int[],     int) = inline nice.lang.inline.ArrayGetOp("i");
long    get(long[],    int) = inline nice.lang.inline.ArrayGetOp("l");
char    get(char[],    int) = inline nice.lang.inline.ArrayGetOp("c");
float   get(float[],   int) = inline nice.lang.inline.ArrayGetOp("f");
double  get(double[],  int) = inline nice.lang.inline.ArrayGetOp("d");

void set(boolean[],int, boolean) = inline nice.lang.inline.ArraySetOp("z");
void set(byte[],   int, byte)    = inline nice.lang.inline.ArraySetOp("b");
void set(short[],  int, short)   = inline nice.lang.inline.ArraySetOp("s");
void set(int[],    int, int)     = inline nice.lang.inline.ArraySetOp("i");
void set(long[],   int, long)    = inline nice.lang.inline.ArraySetOp("l");
void set(char[],   int, char)    = inline nice.lang.inline.ArraySetOp("c");
void set(float[],  int, float)   = inline nice.lang.inline.ArraySetOp("f");
void set(double[], int, double)  = inline nice.lang.inline.ArraySetOp("d");

// Define collection methods

get(a@Array, n) = get(a, n);
set(a@Array, n, e) = set(a,n,e);
size(a@Array) = a.length;

map<C,T,U>(a@Array, f)
{
  int l = a.length;

  U[] res = new U[l];

  for(int i=0; i<l; i=i+1)
    res[i] = f(a[i]);

  return res;
}

filter<C, T>(a@Array, test)
{
  int l = a.length;
  int found = 0;

  T[] res = new T[l];

  a.iter(fun(T elem) =>
	{ if(test(elem)) res.set(found++, elem); } );

  return resize(res, found);
}

/**
   Fills a newly created array with non-null values.

   A typical usage is to allocate a new array and set its values at the same time:
   <code>
     String[] numbers = fill(new String[10], fun(int i)=> "number " + i);
   </code>

   The equivalent code in Java would be:
   <code>
     String[] numbers = new String[10];
     for (int i = 0; i < 10; i++) {
       numbers[i] = "number " + i;
     }
   </code>

   It is important that no reference to the array is kept,
   because that would make it possible to store null values in it.
   There is no danger as long as the array is created inside the call, 
   like it the above example.
*/
<Any T> !T[] fill(?T[] array, int->!T value)
{
  for (int i = 0; i < array.length; i++)
    array[i] = value(i);
  return cast(array);
}
