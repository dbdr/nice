package nice.tools.doc;

/**
   API documentation generator for Nice.

   @author Francis Barber
 */

import nice.doc;
import nice.tools.compiler;
import nice.tools.compiler.console;
import bossa.modules.*;

/**
 * Compilation object allows us to dynamically find out what we are compiling.
 * It is initialised by the main method.
 */
var Compilation compilation = cast(null);

void main(String[] args)
{
  String mainPackage;
  File outdir = new File(".");

  ConsoleOutput consOutput = new ConsoleOutput();
  compilation = new Compilation
    (listener: consOutput, parser: new bossa.parser.JavaccParser(true));

  Program prg = new Program
    (name: "nicedoc", longName: "The Nice Documentation Generator",
     shortDescription: "Documentation Generator for the Nice programming language",
     author: "Daniel Bonniot <bonniot@users.sourceforge.net>\n" +
             "Francis Barber <fbarber@users.sourceforge.net>",
     seeAlso: "http://nice.sourceforge.net          the Nice Home Page\n" +
              "http://nice.sf.net/compilation.html  compilation instructions\n",
     manualSection: "1",
     arguments: "package", options: cast(null));
  prg.options = 
  [
   option("sourcepath",
	  "Search path for source packages\nPATH is a list of directories and .jar archives",
	  "path",
	  String path => { compilation.sourcePath = path; }),

   option("classpath",
	  "Search path for compiled packages and libraries\nPATH is a list of directories and .jar archives",
	  "path",
	  String path => { compilation.packagePath = path; }),
   
   option(letter: 'd', "outdir",
       "Output files to this directory",
       "path",
       String path => {outdir = new File(path); }),

   option("man", "Print man page to stdout",
	  () => man(prg)),

   option("version",
	  "Print version info and exit",
	  () => version()),

   option("usage",
	  "Print usage information and exit",
	  () => usage(prg))
  ];

  List<String> rest = parse(prg, args);
  if (rest.size() != 1 || rest[0].length() == 0)
    {
      if (rest.size() > 1)
	println("Supply only one package on the command line.\n" +
		"nicecdoc will automatically find dependancies.");
      usage(prg, ERROR);
    }
  mainPackage = rest[0];

  /* 
     For convenience of command-line tools (shells) with completions, 
     we treat a directory name as the corresponding package name.
     That is, we replace '/' (which is illegal in package names) by '.'.
  */
  mainPackage = mainPackage.deleteAtEnd("/").replace('/', ".");

  generate(compilation, mainPackage, outdir);
}

